sourceSets.main.java.srcDirs = ["src"]
sourceSets.test.java.srcDirs = ["test"]
sourceSets.test.resources.srcDirs = ["test/resources"]

//set to true when generating code, make sure to clean first
def useParameters = false

import com.badlogic.gdx.jnigen.*

dependencies{
    testImplementation libraries.junit
    testImplementation aproj(":natives:natives-desktop")
}

if(useParameters){
    compileJava{
        options.compilerArgs << '-parameters'
    }
}

test{
    testLogging{
        exceptionFormat = 'full'
    }
}

buildscript{
    dependencies{
        classpath libraries.jnigen
    }
}

task deleteJniFolder(type: Delete) {
    delete "jni"
    delete "docs"
}

clean.dependsOn(deleteJniFolder)

task natives(dependsOn: [classes, deleteJniFolder]){
    doLast{
        def mac = System.getProperty("os.name").toLowerCase().contains("mac")
        def root =  "$project.rootDir/arc-core"
        def jnidir = "$root/jni"
        def libdir = mac ? "$project.rootDir/libs" : "$root/libs"

        new NativeCodeGenerator().generate("$root/src", "$root/build/classes/java/main", jnidir, null, null)

        copy{
            include "**"
            from "csrc/"
            into "jni/"
        }

        //download latest version of stb_image directly into jni folder
        "curl -o $jnidir/stb_image.h https://raw.githubusercontent.com/nothings/stb/master/stb_image.h".execute().waitFor()

        BuildTarget[] targets = [
            BuildTarget.newDefaultTarget(BuildTarget.TargetOs.Windows, false),
            BuildTarget.newDefaultTarget(BuildTarget.TargetOs.Windows, true),
            BuildTarget.newDefaultTarget(BuildTarget.TargetOs.Linux, true),
            BuildTarget.newDefaultTarget(BuildTarget.TargetOs.Android, false),
            BuildTarget.newDefaultTarget(BuildTarget.TargetOs.MacOsX, true).tap{
                cppFlags = "-c -Wall -O2 -arch x86_64 -DFIXED_POINT -fmessage-length=0 -fPIC -mmacosx-version-min=10.7 -stdlib=libc++"
                linkerFlags = "-shared -arch x86_64 -mmacosx-version-min=10.7 -stdlib=libc++"
            },
            BuildTarget.newDefaultTarget(BuildTarget.TargetOs.IOS, false).tap{
                headerDirs = ["iosgl"] as String[]
                cppExcludes = []
            }
        ] as BuildTarget[]

        targets.findAll{it.os != BuildTarget.TargetOs.IOS}*.cppExcludes = ["iosgl/**"] as String[]

        def matches = { String path, List<String> pattern ->
            return pattern.find{ path.contains(it) } != null
        }

        new AntScriptGenerator().generate(new BuildConfig("arc", "$root/build", "libs/", jnidir), targets)

        //overwrite incorrect application mkfile
        new File("$jnidir/Application.mk").text = "APP_ABI := all\nAPP_PLATFORM := android-16"

        //set correct memcpy wrap contents...
        file("$jnidir/memcpy_wrap.c").text = file("$rootDir/natives/memcpy_wrap.c").text
        //wrap functions with right version of glibc
        file("$jnidir/build-linux64.xml").text = file("$jnidir/build-linux64.xml").text.replace("-Wl,-wrap,memcpy", "-Wl,-wrap,memcpy,-wrap,pow")

        for(BuildTarget target : targets){
            if((target.os == BuildTarget.TargetOs.IOS || target.os == BuildTarget.TargetOs.MacOsX) != mac) continue

            String buildFileName = "build-" + target.os.toString().toLowerCase() + (target.is64Bit ? "64" : "32") + ".xml"
            BuildExecutor.executeAnt("$jnidir/" + buildFileName, "-Dhas-compiler=true -Drelease=true clean postcompile")
        }

        new File(libdir).eachFileRecurse{ file ->
            if(!file.path.contains("ios") && !file.path.contains("mac")){
                "strip ${file.absolutePath}".execute().waitFor()
            }
        }

        new File(libdir).eachFileRecurse{ target ->
            if(!target.isDirectory() && matches(target.path, ["windows", "mac", "linux"])){
                file("../natives/natives-desktop/libs/$target.name").bytes = target.bytes
            }else if(!target.isDirectory() && target.path.contains("ios")){
                file("../natives/natives-ios/libs").mkdirs()
                file("../natives/natives-ios/libs/$target.name").bytes = target.bytes
            }
        }

        copy{
            include{ file -> matches(file.path, ["arm", "x86"]) && !file.path.contains("ios") }
            from "libs/"
            into "../natives/natives-android/libs/"
        }

        //cleanup
        delete{ delete "libs" }
        delete{ delete "obj" }
        delete{ delete "out" }
        delete{ delete "jni" }

        //delete extra garbage, not sure why it even exists
        delete{
            delete "../libs"
        }
    }
}
