sourceSets.main.java.srcDirs = ["src"]
sourceSets.main.resources.srcDirs = ["natives"]

import com.badlogic.gdx.jnigen.*

buildscript{
    dependencies{
        classpath "com.badlogicgames.gdx:gdx-jnigen:1.9.10"
    }
}

task deleteJniFolder(type: Delete) {
    delete "jni"
    delete "docs"
}

clean.dependsOn(deleteJniFolder)

task natives(dependsOn: [classes, deleteJniFolder]){
    doLast{

        def root = "$project.rootDir/extensions/discord"
        def jnidir = "$root/jni"
        def libdir = "$root/libs"

        new NativeCodeGenerator().generate("$root/src", "$root/build/classes/java/main", jnidir, null, null)

        def platforms = ["linux", "osx", "win"]

        platforms.each{ platform ->
            def url = "https://github.com/discord/discord-rpc/releases/download/v3.4.0/discord-rpc-${platform}.zip"

            //download each platform
            "curl $url -L -o $jnidir/${platform}.zip".execute().waitFor()

            //unzip each platform
            "unzip $jnidir/${platform}.zip -d $jnidir".execute()
        }

        BuildTarget[] targets = [
            BuildTarget.newDefaultTarget(BuildTarget.TargetOs.Windows, true).tap{
                //cIncludes = cppIncludes = ["discord-rpc/win64-dynamic/include/*.h"]
                libraries += "-L$jnidir/discord-rpc/win64-dynamic/lib -l:discord-rpc.lib"
            },
            BuildTarget.newDefaultTarget(BuildTarget.TargetOs.Linux, true).tap{
                //cIncludes = cppIncludes = ["discord-rpc/linux-dynamic/include/*.h"]
                libraries += "-L$jnidir/discord-rpc/linux-dynamic/lib -l:libdiscord-rpc.so"
            },
            BuildTarget.newDefaultTarget(BuildTarget.TargetOs.MacOsX, true).tap{
                //cIncludes = cppIncludes = ["discord-rpc/osx-dynamic/include/*.h"]
                libraries += "-framework AppKit -L$jnidir/discord-rpc/osx-dynamic/lib -l:libdiscord-rpc.dylib"
            },
        ] as BuildTarget[]

        new AntScriptGenerator().generate(new BuildConfig("arc-discord", "$root/build", "libs/", jnidir), targets)

        //set correct memcpy wrap contents...
        file("$jnidir/memcpy_wrap.c").text = file("$rootDir/natives/memcpy_wrap.c").text
        //wrap functions with right version of glibc
        file("$jnidir/build-linux64.xml").text = file("$jnidir/build-linux64.xml").text.replace("-Wl,-wrap,memcpy", "-Wl,-wrap,memcpy,-wrap,pow")

        for(BuildTarget target : targets){
            if((target.os == BuildTarget.TargetOs.MacOsX) != System.getProperty("os.name").toLowerCase().contains("mac")) continue

            String buildFileName = "build-" + target.os.toString().toLowerCase() + "64.xml"
            BuildExecutor.executeAnt("$jnidir/" + buildFileName, "-Dhas-compiler=true -Drelease=true clean postcompile")
        }

        new File(libdir).eachFileRecurse{ target ->
            if(!target.path.contains("mac")){
                "strip ${target.absolutePath}".execute().waitFor()
            }

            if(!target.isDirectory()){
                file("natives/$target.name").bytes = target.bytes
            }
        }

        //copy discord DLLs
        copy{
            from "jni/discord-rpc/linux-dynamic/lib/libdiscord-rpc.so"
            into "natives/"
        }

        copy{
            from "jni/discord-rpc/win64-dynamic/bin/discord-rpc.dll"
            into "natives/"
        }

        copy{
            from "jni/discord-rpc/osx-dynamic/lib/libdiscord-rpc.dylib"
            into "natives/"
        }

        //cleanup
        delete{ delete "obj" }
        delete{ delete "out" }
        delete{ delete "jni" }
        delete{ delete "libs" }

        //delete extra garbage, not sure why it even exists
        delete{
            delete "../libs"
        }

    }
}